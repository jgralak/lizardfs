# ==========================
#  GoogleTests Framework
# ==========================
if(BUILD_TESTS)
  set(GTEST_VERSION 1.7.0)
  set(GTEST_NAME gtest-${GTEST_VERSION})

  if(NOT IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${GTEST_NAME})
    set(GTEST_ARCHIVE ${GTEST_NAME}.zip)
    set(GTEST_URL http://googletest.googlecode.com/files/${GTEST_ARCHIVE})

    message(STATUS "Downloading ${GTEST_URL}...")
    file(DOWNLOAD
        ${GTEST_URL}
        ${CMAKE_CURRENT_BINARY_DIR}/${GTEST_ARCHIVE}
        INACTIVITY_TIMEOUT 15
        SHOW_PROGRESS
        STATUS DOWNLOAD_STATUS
        EXPECTED_MD5 2d6ec8ccdf5c46b05ba54a9fd1d130d7)

    list(GET DOWNLOAD_STATUS 0 DOWNLOAD_CODE)
    if(NOT DOWNLOAD_CODE STREQUAL 0)
      list(GET DOWNLOAD_STATUS 1 DOWNLOAD_MESSAGE)
      message(FATAL_ERROR "Download ${GTEST_URL} error ${DOWNLOAD_CODE}: ${DOWNLOAD_MESSAGE}")
    endif()

    message(STATUS "Unpacking ${GTEST_ARCHIVE}...")
    execute_process(COMMAND unzip -q ${GTEST_ARCHIVE} -d ${CMAKE_CURRENT_SOURCE_DIR}
      WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
      RESULT_VARIABLE UNZIP_ERROR
      ERROR_VARIABLE UNZIP_ERROR_MESSAGE)
    if(NOT UNZIP_ERROR STREQUAL 0)
    message(FATAL_ERROR "unzip ${GTEST_ARCHIVE} failed: ${UNZIP_ERROR} ${UNZIP_ERROR_MESSAGE}")
    endif()
    if(NOT IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${GTEST_NAME})
      message(FATAL_ERROR "Extracting ${GTEST_ARCHIVE} didn't produce directory '${GTEST_NAME}'")
    endif()
    message(STATUS "Download ${GTEST_NAME} finished successfully")
  else()
    message(STATUS "Found ${GTEST_NAME}")
  endif()

  set(GTEST_INCLUDE_DIR ${GTEST_NAME}/include)
  add_subdirectory(${GTEST_NAME})
endif()
